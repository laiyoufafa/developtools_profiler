# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../../protos.gni")

js_memory_sources = [
  "./js_heap_config.proto",
  "./js_heap_result.proto",
]

#######################################################
proto_out_dir = "$root_gen_dir/cpp/" + rebase_path(".", "//")
proto_rel_out_dir = rebase_path(proto_out_dir, root_build_dir)

js_memory_codegen = []
foreach(proto, js_memory_sources) {
  name = get_path_info(proto, "name")
  js_memory_codegen += [
    "$proto_out_dir/$name.pb.h",
    "$proto_out_dir/$name.pb.cc",
  ]
}

config("js_memory_include_config") {
  include_dirs = [ "$proto_out_dir" ]
}

#######################################################
action("js_memory_cpp_gen") {
  script = "${OHOS_PROFILER_DIR}/build/protoc.sh"
  sources = js_memory_sources
  outputs = js_memory_codegen
  args = [
    "$libc_dir_proto",
    "$root_output_dir_proto",
    "--cpp_out",
    "$proto_rel_out_dir",
    "--proto_path",
    rebase_path(".", root_build_dir),
  ]
  args += rebase_path(sources, root_build_dir)
  deps = [ "${OHOS_PROFILER_3RDPARTY_PROTOBUF_DIR}:protoc(${host_toolchain})" ]
}

source_set("js_memory_cpp") {
  deps = [ ":js_memory_cpp_gen" ]
  public_deps = [
    "${OHOS_PROFILER_3RDPARTY_PROTOBUF_DIR}:protobuf",
    "${OHOS_PROFILER_3RDPARTY_PROTOBUF_DIR}:protobuf_lite",
  ]
  include_dirs = [ "$proto_out_dir" ]
  public_configs = [ ":js_memory_include_config" ]
  sources = js_memory_codegen
}
