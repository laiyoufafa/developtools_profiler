# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../../protos.gni")

all_config_sources = [
  "./all_plugin_common.proto",
  "./all_plugin_config.proto",
]

#######################################################
proto_out_dir = "$root_gen_dir/cpp/" + rebase_path(".", "//")
proto_rel_out_dir = rebase_path(proto_out_dir, root_build_dir)

all_config_codegen = []
foreach(proto, all_config_sources) {
  name = get_path_info(proto, "name")
  all_config_codegen += [
    "$proto_out_dir/$name.pb.h",
    "$proto_out_dir/$name.pb.cc",
  ]
}

config("all_include_config") {
  include_dirs = [ "$proto_out_dir" ]
}

#######################################################
action("all_config_cpp_gen") {
  script = "${OHOS_PROFILER_DIR}/build/protoc.sh"
  sources = all_config_sources
  outputs = all_config_codegen
  args = [
    "$libc_dir_proto",
    "$root_output_dir_proto",
    "--cpp_out",
    "$proto_rel_out_dir",
    "--proto_path",
    rebase_path(".", root_build_dir),
  ]
  args += rebase_path(sources, root_build_dir)
  deps = [ "${OHOS_PROFILER_3RDPARTY_PROTOBUF_DIR}:protoc(${host_toolchain})" ]
}

ohos_source_set("all_config_cpp") {
  deps = [ ":all_config_cpp_gen" ]
  public_deps = [
    "${OHOS_PROFILER_3RDPARTY_PROTOBUF_DIR}:protobuf",
    "${OHOS_PROFILER_3RDPARTY_PROTOBUF_DIR}:protobuf_lite",
  ]
  include_dirs = [ "$proto_out_dir" ]
  public_configs = [ ":all_include_config" ]
  sources = all_config_codegen
}
