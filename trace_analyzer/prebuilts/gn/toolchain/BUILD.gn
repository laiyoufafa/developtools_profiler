# Copyright (C) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

declare_args() {
  if (target_os == "linux") {
    cc = "/usr/bin/clang"
    cxx = "/usr/bin/clang++"
  } else if (target_os == "windows") {
    cc = "~/mingw-w64/ohos/linux-x86_64/clang-mingw/bin/clang"
    cxx = "~/mingw-w64/ohos/linux-x86_64/clang-mingw/bin/clang++"
  }
}

toolchain("gcc_like") {
  lib_switch = "-l"
  lib_dir_switch = "-L"

  tool("cxx") {
    depfile = "{{output}}.d"
    command = "$cxx -MMD -MF $depfile {{defines}} -fPIC {{include_dirs}} {{cflags}} {{cflags_cc}} -c {{source}} -o {{output}}"
    depsformat = "gcc"
    outputs =
        [ "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o" ]
    description = "compile {{source}}"
  }

  tool("cc") {
    depfile = "{{output}}.d"
    command = "$cc -MMD -MF $depfile {{defines}} -fPIC {{include_dirs}} {{cflags}} {{cflags_c}} -c {{source}} -o {{output}}"
    depsformat = "gcc"
    outputs =
        [ "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o" ]
    description = "compile {{source}}"
  }

  tool("link") {
    command = "$cxx {{ldflags}} {{inputs}} {{solibs}} {{libs}} -o {{output}}"
    outputs = [ "{{root_out_dir}}/{{target_output_name}}{{output_extension}}" ]
    description = "link {{output}}"
  }
  tool("solink") {
    default_output_extension = ".so"
    command =
        "$cxx {{ldflags}} {{inputs}}  {{solibs}} {{libs}} -shared -o {{output}}"
    outputs = [ "{{root_out_dir}}/{{target_output_name}}{{output_extension}}" ]
    description = "solink {{output}}"
  }

  tool("stamp") {
    command = "touch {{output}}"
    description = "touch {{output}}"
  }

  tool("copy") {
    if (is_win) {
      command = "cp -af {{source}}* {{output}}.exe"
      description = "COPY {{source}}.exe {{output}}.exe"
    } else {
      command = "cp -af {{source}} {{output}}"
      description = "COPY {{source}} {{output}}"
    }
  }
}
